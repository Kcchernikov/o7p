(* Интерактивный график функции *)
MODULE Init;

 IMPORT D := Draw, M := Math, S := Strings;

 VAR
  label1, label2: ARRAY 64 OF CHAR;
  labelColor: INTEGER;
  pressed: BOOLEAN;
  pos: REAL;

 PROCEDURE Setup;
 BEGIN
  D.SetSize(640,360);
  D.SetFrameRate(20);
  pos := 40.0;
 END Setup;

 PROCEDURE Pressed;
 BEGIN pressed := (D.mouseX > 474.0) & (D.mouseX < 486.0);
 END Pressed;

 PROCEDURE Released;
 BEGIN
  pressed := FALSE
 END Released;

 PROCEDURE Plot;
 VAR x0, y0, x1, y1, x, w: REAL; i: INTEGER;
 BEGIN
  D.SetBackground(255H);
  
  (* Слайдер *)
  IF pressed THEN
   labelColor := 0FFFF77FFH; (* ARGB формат *)
   pos := D.mouseY;
   IF pos < 40.0 THEN pos := 40.0 END;
   IF pos > 320.0 THEN pos := 320.0 END
  ELSE
   labelColor := 0H
  END;

  D.Stroke(70H);
  D.StrokeWidth(2);
  D.Line(480.0, 40.0, 480.0, 320.0);
  D.Stroke(0H);
  D.Fill(labelColor);
  D.RectRounded(474.0, pos - 6.0, 12.0, 12.0, 4.0);
  D.Fill(0H);
  D.Stringi("1", 475, 25, 16);
  D.Stringi("10", 470, 345, 16);
  w := 1.0 + (pos - 40.0) / 280.0 * 9.0;
  label1 := "ω = ";
  S.RealToString(w, label2, 1);
  S.Join(label1, label2);
  D.Stringi(label1, 500, FLOOR(pos) + 5, 16);

  (* Оси координат *)
  D.Stroke(0H);
  D.Line(200.0, 20.0, 200.0, 300.0);
  D.Line(200.0, 20.0, 195.0, 40.0);
  D.Line(200.0, 20.0, 205.0, 40.0);
  D.Line(0.0, 200.0, 450.0, 200.0);
  D.Line(450.0, 200.0, 430.0, 205.0);
  D.Line(450.0, 200.0, 430.0, 195.0);

  (* График функции *)
  D.Stroke(0FF7777FFH);
  x1 := 0.0;
  x  := - M.Pi();
  y1 := 200.0 - M.Cos(x * w) * 100.0;

  FOR i := 0 TO 200 DO
   x0 := x1;
   y0 := y1;
   x1 := FLT(i * 2);
   x  := - M.Pi() + (FLT(i * 2)) / 400.0 * 2.0 * M.Pi();
   y1 := 200.0 - M.Cos(x * w) * 100.0;
   D.Line(x0, y0, x1, y1)
  END;

  (* Подписи *)
  D.Fill(0H);
  D.Stringi(" 1", 180, 100, 16);
  D.Stringi("-1", 180, 300, 16);
  D.Stringi("-π", 0, 220, 16);
  D.Stringi("-π/2", 90, 220, 16);
  D.Stringi(" π/2", 280, 220, 16);
  D.Stringi(" π", 400, 220, 16);
  D.Stringi("y = Cos(ωx)", 20, 40, 16);
 END Plot;

BEGIN
 D.CreateCanvas("log");
 D.SetSetupProc(Setup);
 D.SetDrawProc(Plot);
 D.SetPressedProc(Pressed);
 D.SetReleasedProc(Released);
 D.Start
END Init.
