(* Мать и крокодил *)
MODULE Init;
IMPORT Log, D := Draw2;
 
CONST
  bgColor = 102; btnSelected = 255; btnUnselected = 150;
  statementColor = 255; goodColor = 0FF00CC00H; badColor = 0FFFF0000H;
 
VAR
  state1, state2, lastb: INTEGER;
  pressed: BOOLEAN;
 
PROCEDURE Setup;
BEGIN
  D.SetSize(600, 400);
  state1 := 0; state2 := 0;
END Setup;

PROCEDURE Button(caption: ARRAY OF CHAR; x, y, w, h, state, nstate: INTEGER; pressable: BOOLEAN);
  VAR rx, ry, rw, rh: REAL;
BEGIN
  rx := FLT(x); ry := FLT(y); rw := FLT(w); rh := FLT(h);
  D.NoFill();
  IF pressable THEN
    D.Stroke(0)
  ELSE
    D.Stroke(btnUnselected)
  END;
  D.Rect(rx, ry, rw, rh);
  IF pressable THEN
    IF (D.MouseX >= rx) & (D.MouseX <= rx+rw) & (D.MouseY >= ry) & (D.MouseY <= ry+rh) THEN
  	  D.Rect(rx+5.0, ry+5.0, rw-10.0, rh-10.0);
      lastb := nstate
    END
  END;
  IF state = nstate THEN 
  	D.String(caption, x+10, y+30, 20)
  ELSE
    D.Fill(btnUnselected);
  	D.String(caption, x+10, y+30, 20);
    D.Fill(btnSelected);
  END
END Button;

PROCEDURE DrawButtons;
BEGIN
  Button("Вернешь", 30, 170, 100, 50, state1, 1, TRUE);
  Button("Не вернешь", 235, 170, 120, 50, state1, 2, TRUE);
  Button("Неизвестно", 455, 170, 125, 50, state1, 3, TRUE);
  IF state1 = 2 THEN
    Button("Вернет", 30, 270, 100, 50, state2, 4, TRUE);
    Button("Не вернет", 235, 270, 120, 50, state2, 5, TRUE);
  END
END DrawButtons;

PROCEDURE ChangeState;
BEGIN
  IF lastb > 3 THEN
    state1 := 2;
    state2 := lastb;
  ELSE
    state1 := lastb;
    state2 := 0
  END
END ChangeState;
 
PROCEDURE Draw;
BEGIN
  D.Translate(2.0, 2.0);
  D.SetBackground(bgColor);
  lastb := 0;
  D.SetFont("Times New Roman");
  D.Fill(statementColor);
  D.String("Крокодил:", 30, 35, 28);
  D.String("'Верну ребенка, если угадаешь,", 30, 70, 28);
  D.String(" верну ли я тебе ребенка, или нет'", 30, 105, 28);
  D.String("Мать ребенка:", 30, 150, 28);
  DrawButtons;
  IF pressed THEN
    pressed := FALSE;
    ChangeState;
    DrawButtons
  END;
  IF state1 = 1 THEN
    D.Fill(badColor);
    D.String("Не угадала - не вернет и съест", 125, 250, 25);
  ELSIF state1 = 2 THEN
    D.Fill(goodColor);
    D.String("Что теперь решит крокодил?", 135, 250, 25);
    IF state2 = 4 THEN
      D.String("Значит, он вернет ребенка", 135, 350, 25);
    ELSIF state2 = 5 THEN
      D.String("Значит, мать угадала и выиграла", 125, 350, 25);
    END
  ELSIF state1 = 3 THEN
    D.Fill(badColor);
    D.String("Съест ребенка и никому не скажет", 110, 250, 25)
  END
END Draw;

PROCEDURE Pressed;
BEGIN
  pressed := TRUE;
END Pressed;
 
PROCEDURE Do*;
BEGIN
  D.CreateCanvas("log");
  D.SetSetupProc(Setup);
  D.SetDrawProc(Draw);
  D.SetPressedProc(Pressed);
  D.Start
END Do;

BEGIN
Do
END Init.
